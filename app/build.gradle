apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'

android {
    compileSdk 33

    // Travis update separately
    buildToolsVersion '33.0.1'

    defaultConfig {
        applicationId "com.atrainingtracker"
        minSdk 19
        targetSdk 33
        multiDexEnabled true
        versionName "4.2.5"
        versionCode 217
    }

    signingConfigs {
        //noinspection GroovyMissingReturnStatement
        release {
            // storeFile file('/home/rainer/Dropbox/workspace/keystore')
            // keyAlias "aTrainingTrackerReleaseKey"
        }
    }

    buildTypes {
        debug {
            // Separate the debug build
            applicationIdSuffix ".debug"
        }
        release {
            applicationIdSuffix ""

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
            zipAlignEnabled true
            debuggable false
        }
    }

    packagingOptions {
        resources {
            pickFirsts += ['META-INF/NOTICE.txt', 'META-INF/NOTICE', 'META-INF/DEPENDENCIES', 'META-INF/LICENSE.txt', 'META-INF/LICENSE']
        }
    }


    useLibrary 'org.apache.http.legacy'

    namespace 'com.atrainingtracker'
    lint {
        baseline file('lint-baseline.xml')
        checkReleaseBuilds true
        disable 'DuplicatePlatformClasses'
        showAll true
        textOutput file('stdout')
        textReport true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

configurations {
    compile.exclude group: "junit", module: "junit"
}

configurations.all {
    resolutionStrategy.dependencySubstitution {
        substitute module('org.hamcrest:hamcrest-core:1.1') using module('junit:junit:4.10')
    }
}

dependencies {
    implementation group: 'commons-codec', name: 'commons-codec', version: '1.15'
    implementation group: 'commons-io', name: 'commons-io', version: '1.2'
    implementation group: 'commons-logging', name: 'commons-logging', version: '1.2'

    implementation group: 'org.apache.httpcomponents', name: 'fluent-hc', version: '4.5.14'
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.14'
    implementation group: 'org.apache.httpcomponents', name: 'httpclient-cache', version: '4.5.14'
    implementation group: 'org.apache.httpcomponents', name: 'httpcore', version: '4.4.16'
    implementation group: 'org.apache.httpcomponents', name: 'httpmime', version: '4.5.14'

    implementation group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1'
    implementation 'com.getpebble:pebblekit:4.0.1'
    implementation files('../ANT-Android-SDKs/ANT+_Android_SDK/API/antpluginlib_3-8-0.aar')

    implementation 'com.opencsv:opencsv:4.6'
    implementation 'androidx.preference:preference:1.2.0'
    implementation 'androidx.appcompat:appcompat:1.6.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'com.google.android.material:material:1.9.0-alpha01'
    implementation 'com.google.android.gms:play-services-maps:18.1.0'
    implementation 'com.google.android.gms:play-services-location:21.0.1'

    implementation 'com.dropbox.core:dropbox-core-sdk:3.1.3'

    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

    def lifecycle_version = "2.5.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
}

apply plugin: 'com.google.gms.google-services'

def props = new Properties()
if (rootProject.file("release.properties").exists()) {
    props.load(new FileInputStream(rootProject.file("release.properties")))

    android.signingConfigs.release.storeFile rootProject.file(props.keyStore)
    android.signingConfigs.release.storePassword props.keyStorePassword
    android.signingConfigs.release.keyAlias props.keyAlias
    android.signingConfigs.release.keyPassword props.keyAliasPassword
} else {
    project.logger.info('INFO: Set the values storeFile, storePassword, keyAlias, and keyPassword in release.properties to sign the release.')
    android.buildTypes.release.signingConfig = null
}
